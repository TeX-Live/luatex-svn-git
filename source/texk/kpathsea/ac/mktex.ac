## texk/kpathsea/ac/mktex.ac: configure.ac fragment for the TeX Live
## subdirectory texk/kpathsea.
dnl
dnl Copyright (C) 2009, 2011 Peter Breitenlohner <tex-live@tug.org>
dnl You may freely use, modify and/or distribute this file.
dnl
## configure defaults for mktexfmt & Co.
m4_define_default([kpse_indent_26], [26])[]dnl
m4_define([kpse_mktex_opts], [dnl
[[mktexmf],  [yes], [MF source],   [TEX_MF]],
[[mktexpk],  [yes], [PK font],     [TEX_PK]],
[[mktextfm], [yes], [TFM file],    [TEX_TFM]],
[[mkocp],    [yes], [OCP file],    [OMEGA_OCP]],
[[mkofm],    [yes], [OFM file],    [OMEGA_OFM]],
[[mktexfmt], [yes], [format file], [TEX_FMT]],
[[mktextex], [no],  [TeX source],  [TEX_TEX]],
])[]dnl
m4_foreach([Kpse_Mktex], [kpse_mktex_opts],
           [m4_ifset([Kpse_Mktex],
                     [KPSE_WITH_MKTEX(Kpse_Mktex)])])[]dnl
